import Alert from '@mui/material/Alert'
import Accordion from '@mui/material/Accordion';
import AccordionDetails from '@mui/material/AccordionDetails';
import AccordionSummary from '@mui/material/AccordionSummary';
import ArrowDropDownIcon from '@mui/icons-material/ArrowDropDown';

# Deploying Modules

## Prerequisites

This guide assumes you have prior experience working with infrastructure tools such as
terraform and terragrunt. If you are not, please review the concept docs (TODO)
before proceeding.

## Setting up Your Repo

<Alert severity='info'>
    We provide an example of a fully configured reference repository [here](https://github.com/Panfactum/stack/tree/main/packages/reference).
</Alert>

In order to provide a convenient and integrated experience for consumers of the Panfactum stack, we
assume you will configure your stack repository inline with the following guidelines.

### Version Control

One core axiom is that the stack repo will follow [Github Flow](https://githubflow.github.io/) for creating
and integrating new changes.

The most important facets of Github flow are:

- There is exactly **one** primary integration branch
- That branch is **always** deployable

We extend that pattern with one more rule:

- That branch contains the configuration-as-code for **all** deployed infrastructure across **every**
environment

This is a **critically important** concept. Again, **one** branch will define exactly what is deployed across
**all** environments.

Pick the branch you want to use and make sure it is set as the `PF_REPO_PRIMARY_BRANCH` (per the [docs](../../reference/repo-variables)).

### Environments Folder

We store all configuration-as-code for all environment in a directory in the repo we canonically call
the "environments folder."

Pick the folder you want to use and make sure it is set as the `PF_ENVIRONMENTS_DIR` (per the [docs](../../reference/repo-variables)).

## Terragrunt Setup

Terragrunt is the utility that handles installing and deploying terraform modules. You control how terragrunt
behaves via `hcl` and `yaml` files defined in the environments folder.

### Panfactum Codegen

We provide some standard terragrunt boilderplate that we assume you will use. This
code can be installed by running `pf-update-terragrunt`.

This will create the following files:

- `panfactum.hcl`: Standard terragrunt that should be included in every module
- `providers/*`: Terraform provider configuration for the providers using the Panfactum stack
- `.gitignore`: Defines filetypes that should not be committed to version control if following our conventions

**The rest of the setup must be completed by you.**

### Setting Up Directory Structure

Your environments folder MUST have three levels of scoping for the configuration-as-code:

- environment: A separation of your deployments according to your SDLC conventions (e.g., `development`, `staging`, `production`, etc.)
- region: A separation of your deployments according to their physical location (e.g., `us-west-2`)
- module: A separation of your deployments based on the specific terraform module being deployed

Accordingly, there are four levels of configuration for parameterizing the behavior of `panfactum.hcl`:

- `global.yaml`: Found at the root of the directory
- `environment.yaml`: Found at the root of each environment subdirectory
- `region.yaml`: Found at the root of each region subdirectory
- `module.yaml`: Found in each module subdirectory

A few rules about these files:

- These files are optional, but you will use them to configure your providers and other behavior (described in detail later).
- They can each contain *any and every key* found in this [reference doc](../../reference/terragrunt-variables).
- You should commit these files to version control.
- Conflicts are resolved via specificity (e.g., conflicting `module.yaml` values will override `region.yaml` values which in turn will override `environment.yaml` values).
- Each file has a optional, user-specific counterpart named `*.user.yaml` (e.g., `region.user.yaml`).
    - Values in these files override the values in their counterparts for local development purposes (e.g., `region.user.yaml` overrides values in `region.yaml`).
    - These files are not committed to version-control; each user will have their own set of values.

**At this point, you should scaffold your environments folder until it looks something like this:**

```text
- environments/
    - global.yaml
    - panfactum.hcl
    - providers/
      - aws.tf
      - kubernetes.tf
      ***
    - [environment_1]/
      - environment.yaml
      - environment.user.yaml
      - [region_1]/
        - region.yaml
        - region.user.yaml
        - [module_1]/
          - module.yaml
        - [module_2]/
          - module.yaml
      - [region_2]/
        ***
    - [environment_2]/
      - environment.yaml
      - [region_1]/
         ***
      - [region_2]/
        ***

```

## Defining a Module for Deployment

When defining terraform modules for deployment, you should add a `terragrunt.hcl` file to each module
folder. All utilities available in terragrunt are available to you.

To take advantage of the Panfactum stack, your `terragrunt.hcl` must at minimum have the following `include`
block:

```hcl
include "panfactum" {
  path = find_in_parent_folders("panfactum.hcl")
  expose = true
}
```

You will also likely want to include a `terraform` source such as the following:

```hcl
terraform {
  source = "github.com/Panfactum/stack.git/packages/terraform//aws_eks"
}
```

This will differ slightly depending on whether you are using an [third-party module](../third-party-modules) or
a [first-party module](../first-party-modules).

Finally, you will likely want an `inputs` field to define the provided inputs to the sourced terraform module:

```hcl
inputs = {
  foo = "bar"
}
```

A full file example might look something like this:

<Accordion>
<AccordionSummary expandIcon={<ArrowDropDownIcon />} aria-controls="terragrunt-hcl" id="terragrunt-hcl">
    `[environment_1]/[region_1]/[module_1]/terragrunt.hcl`
</AccordionSummary>
<AccordionDetails>
    ```hcl
    include "panfactum" {
      path = find_in_parent_folders("panfactum.hcl")
      expose = true
    }

    terraform {
       source = "github.com/Panfactum/stack.git/packages/terraform//aws_eks"
    }

    inputs = {
      foo = "bar"
    }
    ```
</AccordionDetails>
</Accordion>

### Setting up Providers

**Before you deploy a module**, you must setup your [terraform providers](https://developer.hashicorp.com/terraform/language/providers).

We provide ready-made configuration for the providers used in the Panfactum stack:

- `aws`
- `helm`
- `kubernetes`
- `local`
- `random`
- `time`
- `tls`
- `vault`

To enable a provider, add the provider under `providers` key of the module's `module.yaml`.

For example, this `module.yaml` would enable the `aws` and `kubernetes` provider for a module:

```yaml
providers:
  - aws
  - kubernetes
```

Some providers require extra configuration variables. To view each provider's required
configuration variables, see the [reference docs](../../references/terragrunt-variables).

Generally, you will want to configure a provider on an environment-wide or region-wide basis (i.e.,
in the `environment.yaml` or `region.yaml` files).

### Using Secrets

TODO


## Deploying Modules

Once you have configured your module, you can use `terragrunt` to deploy the module.

Terragrunt wraps `terraform` CLI and contains the same subcommands (e.g., `apply`, `plan`, etc.). Simply
change to the module subdirectory in your environments folder and run the desired command  (e.g., `terragrunt apply`).

### Defining the Dependency Graph

Typically, there will be an order in which your infrastructure modules in any particular environment
must be deployed. For example, you will need to define your Kubernetes cluster before you can deploy
Helm charts to it.

You should explicitly declare these dependencies via terragrunt `dependency` blocks ([docs](https://terragrunt.gruntwork.io/docs/reference/config-blocks-and-attributes/#dependency)).

This will ensure that modules are always updated in the desired order. Additionally, you can use this functionality
to pass the outputs of one module into the inputs of another.

### CI / CD

TODO

## Customizing the Default Behavior

While our `panfactum.hcl` provides a lot of functionality out-of-the-box, you may want to add or change
functionality on a global basis.

To accomplish this, define your own `shared.hcl` with its own behavior in the **root** of the environments folder.

Add the following `include` block to **each** module's `terragrunt.hcl` **after** the `"panfactum"` `include`:

```hcl
include "shared" {
  path = find_in_parent_folders("shared.hcl")
}
```

This will merge your custom settings with the Panfactum default settings (see the
[docs](https://terragrunt.gruntwork.io/docs/reference/config-blocks-and-attributes/#include) for different merge strategies).

### Accessing the Scoped YAML Variables
In your `shared.hcl`, you may find it helpful to reference values defined in the `global.yaml`, `environment.yaml`,
`region.yaml`, and `module.yaml` files, **especially because you can add arbitrary values to these files**.


Unfortunately, it is not currently possible to import the resolved values
directly from the `panfactum.hcl` into your `shared.hcl` due to [this outstanding issue](https://github.com/gruntwork-io/terragrunt/issues/1566).

Until that issue is resolved, you should copy this block into your `shared.hcl`:

<Accordion>
    <AccordionSummary expandIcon={<ArrowDropDownIcon />} aria-controls="locals-snippet" id="locals-snippet">
        `locals` snippet
    </AccordionSummary>
    <AccordionDetails>
        ```hcl
        locals {
            global_file = find_in_parent_folders("global.yaml", "DNE")
            global_raw_vars = local.global_file != "DNE" ? yamldecode(file(local.global_file)) : {}
            global_user_file = find_in_parent_folders("global.user.yaml", "DNE")
            global_user_vars = local.global_user_file != "DNE" ? yamldecode(file(local.global_user_file)) : {}
            global_vars = merge(local.global_raw_vars, local.global_user_vars)

            environment_file = find_in_parent_folders("environment.yaml", "DNE")
            environment_raw_vars = local.environment_file != "DNE" ? yamldecode(file(local.environment_file)) : {}
            environment_user_file = find_in_parent_folders("environment.user.yaml", "DNE")
            environment_user_vars = local.environment_user_file != "DNE" ? yamldecode(file(local.environment_user_file)) : {}
            environment_vars = merge(local.environment_raw_vars, local.environment_user_vars)

            region_file = find_in_parent_folders("region.yaml", "DNE")
            region_raw_vars = local.region_file != "DNE" ? yamldecode(file(local.region_file)) : {}
            region_user_file = find_in_parent_folders("region.user.yaml", "DNE")
            region_user_vars = local.region_user_file != "DNE" ? yamldecode(file(local.region_user_file)) : {}
            region_vars = merge(local.region_raw_vars, local.region_user_vars)

            module_file      = "${get_terragrunt_dir()}/module.yaml"
            module_raw_vars    = fileexists(local.module_file) ? yamldecode(file(local.module_file)) : {}
            module_user_file      = "${get_terragrunt_dir()}/module.user.yaml"
            module_user_vars    = fileexists(local.module_user_file) ? yamldecode(file(local.module_user_file)) : {}
            module_vars = merge(local.module_raw_vars, local.module_user_vars)

            # Merge all of the vars with order of precedence
            vars = merge(
                local.global_vars,
                local.environment_vars,
                local.region_vars,
                local.module_vars
            )
        }
        ```
    </AccordionDetails>
</Accordion>

You will then be able to access the properly scoped variable via `local.vars` at other places in your `shared.hcl` configuration.

### Supporting Additional Providers

One common customization will be adding additional terraform providers for your organization's
custom terraform modules. We recommend aligning your provider configuration setup with how Panfactum
defines and enables our built-in providers. This conforms to the official
[terragrunt recommendation](https://terragrunt.gruntwork.io/docs/features/keep-your-terraform-code-dry/#dry-common-terraform-code-with-terragrunt-generate-blocks)
for managing providers.

Let's assume that you want to build modules that require the [Datadog terraform provider](https://registry.terraform.io/providers/DataDog/datadog/latest/docs), and you want all modules
to use the same provider settings.

1. Ensure that each module has an `include` block for your custom `shared.hcl` (see above).
2. Follow the "Accessing the Scoped YAML Variables" to gain access to the `local.vars.providers` array in your `shared.hcl` configuration.
3. Create a new `datadog.tf` file with the following contents (example). Add it to the `environments/providers/` directory
alongside the other provider snippets.

    ```hcl
    provider "datadog" {
      api_url = "https://api.datadoghq.com/"
    }
    ```

4. Add the following snippet to your `shared.hcl`:

    ```hcl
    generate "datadog_provider" {
        path      = "datadog.tf"
        if_exists = "overwrite_terragrunt"
        contents  = contains(local.vars.providers, "datadog") ? file("${path_relative_from_include()}/providers/datadog.tf") : ""
    }
    ```
5. Add `datadog` to your `module.yaml` `providers` array:

    ```yaml
    providers:
        - aws      # Will use the built-in Panfactum aws provider config
        - datadog  # Will use your new Datadog provider config
    ```

## Next Steps

- [Learn how to deploy Panfactum terraform modules (and other third-party modules)](./third-party-modules)
- [Learn how to develop and deploy your own Terraform modules](./first-party-modules)

